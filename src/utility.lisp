;;;; utility.lisp

(defpackage #:cl-99.utility
  (:use #:cl))
(in-package #:cl-99.utility)

(cl-annot.syntax:enable-annot-syntax)

;;; define common utilities for cl-99 problems

@export
(defmacro with-gensyms ((&rest names) &body body)
  "Generates a globally unique symbol then executes the body."
  `(let ,(loop for n in names collect `(,n (gensym)))
     ,@body))

@export
(defmacro once-only ((&rest names) &body body)
  "Generates macro that loads the input symbols only once."
  (let ((syms (loop for name in names collect (gensym))))
    `(let (,@(loop for sym in syms collect `(,sym (gensym))))
       `(let (,,@(loop for sym in syms for name in names collect ``(,,sym ,,name))) 
          ,(let (,@(loop for name in names for sym in syms collect `(,name ,sym))) 
             ,@body)))))

@export
(defmacro execute-macro (macro-name &rest args)
  "Execute the macro generated by the macro that generates macro."
  (with-gensyms (new-macro)
    `(progn
       (defmacro ,new-macro ()
         (,macro-name ,@args))
       (,new-macro))))

@export
(defun duplicate (cnt elem)
  "Generates a list which contains <cnt> duplications of <elem>."
  (loop repeat cnt collect elem))

@export
(defun join (func lst)
  "applies <func> on each element of <lst> and join the result lists to a single one"
  (reduce #'append (mapcar func lst)))

@export
(defun symbol-function-or-nil (symbol)
  (if (and (fboundp symbol)
           (not (macro-function symbol))
           (not (special-operator-p symbol)))
    (symbol-function symbol)
    nil))
